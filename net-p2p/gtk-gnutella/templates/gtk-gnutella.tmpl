# {{ autogen_id }}
# Distributed under the terms of the GNU General Public License v2

EAPI=7

inherit flag-o-matic toolchain-funcs

IUSE="nls dbus ssl +gtk"

DESCRIPTION="A GTK+ Gnutella client"
HOMEPAGE="http://gtk-gnutella.sourceforge.net/"
SRC_URI="{{ artifacts[0].src_uri }}"

SLOT="0"
LICENSE="CC-BY-SA-4.0 GPL-2"
KEYWORDS="*"

RDEPEND="
	sys-libs/binutils-libs:=
	dev-libs/glib:2
	sys-libs/zlib
	gtk? ( >=x11-libs/gtk+-2.2.1:2 )
	dbus? ( >=sys-apps/dbus-0.35.2 )
	ssl? ( >=net-libs/gnutls-2.2.5 )
	nls? ( >=sys-devel/gettext-0.11.5 )"
DEPEND="${RDEPEND}"
BDEPEND="virtual/pkgconfig"

src_prepare() {
	filter-flags -flto

	echo "# Gentoo-selected LINGUAS" > po/LINGUAS
	for ling in ${LINGUAS}; do
		echo $ling >> po/LINGUAS
	done

	default
}

src_configure() {
	# There is no option to turn off optimization through the build.sh
	# script.
	sed -i -e "s/Configure -Oder/Configure -Oder -Doptimize=none/" build.sh || die

	# The build script does not support the equivalent --enable
	# options so we must construct the configuration by hand.

	local myconf=(
		$(usev gtk '--gtk2' '--topless')
		$(usev !dbus '--disable-dbus' '')
		$(usev !nls '--disable-nls' '')
		$(usev !ssl '--disable-gnutls' '')
	)

	./build.sh \
		--configure-only \
		--prefix="/usr" \
		--cc="$(tc-getCC)" \
		${myconf[@]} || die "configure failed"
}

src_compile() {
	# Build system is not parallel-safe
	emake -j1
}

src_install() {
	dodir /usr/bin
	emake INSTALL_PREFIX="${D}" install
	dodoc AUTHORS ChangeLog README TODO

	# Touch the symbols file into the future to avoid warnings from
	# gtk-gnutella later on, since we will most likely strip the binary.
	touch --date="next minute" "${D}/usr/lib/gtk-gnutella/gtk-gnutella.nm" || die
}
